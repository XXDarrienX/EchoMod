# Husk Armies
husk_army = {
	damage = 0.5
	health = 0.5
	collateral_damage = 1.25
	war_exhaustion = 0.0
	time = 30
	icon_frame = 7
    pop_limited = no
    has_morale = no

	resources = {
		category = armies
		cost = {
			soul_crystals = 1
		}
	}

	prerequisites = { "tech_assault_armies" }
	
	show_tech_unlock_if = { has_civic = civic_vb_psi_soul_eater }
	
	potential = {
		from = {
			is_enslaved = yes
			NOR = {
				has_trait = "trait_mechanical"
                has_trait = "trait_machine_unit"
                has_trait = "trait_vb_psi_soul_eater"
				is_sapient = no
			}
		}
		owner = {
			has_civic = civic_vb_psi_soul_eater
		}
	}
}

# Soul Hunter
soul_hunter = {
	damage = 3.0
	morale_damage = 3
	health = 3
	morale = 3
	collateral_damage = 3
	war_exhaustion = 3.0
	time = 120
	icon_frame = 9

	resources = {
		category = armies
		cost = {
			soul_crystals = 300
		}

		upkeep = {
			soul_crystals = 3
		}
	}

	prerequisites = { "tech_assault_armies" }
	potential = {
		from = {
            has_trait = "trait_vb_psi_soul_eater"
		}
		owner = {
			has_civic = civic_vb_psi_soul_eater
		}
    }
    show_tech_unlock_if = { has_civic = civic_vb_psi_soul_eater }
}